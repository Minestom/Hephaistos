plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

group 'org.jglrxavpok.nbt'
version '1.1.1'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        kotlin {
            srcDir 'src/main/kotlin'
        }
        java {
            srcDir 'src/antlr/gen'
        }
    }
    gsonBased {
        kotlin {
            srcDir 'src/gson/kotlin'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
    gsonTest {
        java {
            srcDir 'src/gsontest/java'
        }
        compileClasspath += sourceSets.gsonBased.runtimeClasspath
        runtimeClasspath += sourceSets.gsonBased.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

java {
    registerFeature("gson") {
        usingSourceSet(sourceSets.gsonBased)
        withJavadocJar()
        withSourcesJar()
    }

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    gsonBasedImplementation 'com.google.code.gson:gson:2.8.6'

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.8-1'

    // https://mvnrepository.com/artifact/junit/junit
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
compileGsonBasedKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
compileGsonTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.MultiPlatform"]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}